plugins {
  id 'org.springframework.boot' version '3.2.1'
  id 'io.spring.dependency-management' version '1.1.4'
  id 'com.github.spotbugs' version '6.0.6'
  id 'java'
  id 'groovy'
  id 'pmd'
  id 'checkstyle'
  id 'idea'
}

group = 'pl.recruitment'
version = '0.0.1-SNAPSHOT'

java {
  sourceCompatibility = '17'
}

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.flywaydb:flyway-core'
  implementation "net.javacrumbs.shedlock:shedlock-spring:${shedlockVersion}"
  implementation "net.javacrumbs.shedlock:shedlock-provider-jdbc-template:${shedlockVersion}"

  compileOnly 'org.projectlombok:lombok'

  runtimeOnly 'org.postgresql:postgresql'

  annotationProcessor 'org.projectlombok:lombok'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation "org.spockframework:spock-core:${spockVersion}"
  testImplementation "org.spockframework:spock-spring:${spockVersion}"
  testImplementation "org.apache.groovy:groovy-all:${groovyVersion}"
  testImplementation "org.assertj:assertj-core:${assertjVersion}"
  testImplementation "org.testcontainers:postgresql:${testcontainersVersion}"
}

spotbugs {
  toolVersion = '4.8.0'
  showProgress = false
  excludeFilter = file('config/spotbugs/exclude.xml')
}

pmd {
  ruleSetFiles = files("config/pmd/ruleset.xml")
  ruleSets = []
  ignoreFailures = false
}

checkstyle {
  toolVersion = '10.9.3'
  showViolations = true
  maxWarnings = 0
}

spotbugsMain {
  reports {
    html.required = true
    xml.required = false
  }
}

tasks.withType(Checkstyle).configureEach {
  reports {
    xml.required = false
    html.required = true
  }
}

tasks.named('test') {
  useJUnitPlatform()
}
